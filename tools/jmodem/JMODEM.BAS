10 CLS:PRINT "JMODEM by Joshua Foster"
20 DEFINT A-Z
30 DIM FBUF(16384),SBUF(131)
40 NF=0:TOTALPAKS=0
50 PRINT "Opening serial port...";
60 OPEN "COM2:1200,N,8,1,CS,DS,CD" AS #1
70 PRINT "Waiting for data..."
80 ' Receive packet
90 NS=0
100 WHILE NS<131
110   IF EOF(1) THEN 110
120   A$=INPUT$(LOC(1),#1)
130   FOR I=0 TO LEN(A$)-1
140     SBUF(NS+I)=ASC(MID$(A$,I+1,1))
150   NEXT I
160   NS=NS+I
170 WEND
180 REM FOR I=126 TO 130: PRINT SBUF(I);: NEXT I
190 ' Verify checksum
200 GOSUB 490
210 IF NOT VALID THEN PRINT "Checksum failed, trying again";:PRINT #1,CHR$(21);:GOTO 80
220 ' Verify packet number
230 PAKNUM=SBUF(1)
240 IF PAKNUM<>TOTALPAKS THEN PRINT "Expected packet"TOTALPAKS"but received"PAKNUM"!":CLOSE:END
250 ' Special handling for header packet
260 IF PAKNUM=0 THEN GOSUB 540 ELSE OFFSET=2
270 ' Copy packet data to filebuf ---
280 TOCOPY=SIZE-NF:IF TOCOPY>(130-OFFSET) THEN TOCOPY=(130-OFFSET)
290 FOR I=0 TO TOCOPY
300   FBUF(NF+I)=SBUF(OFFSET+I)
310 NEXT I
320 TOTALPAKS=TOTALPAKS+1
330 NF=NF+TOCOPY
340 PRINT NF"...";
350 'Ack the packet and recv the next one
360 PRINT #1,CHR$(6);
370 IF NF<SIZE THEN GOTO 80
380 PRINT "Writing file to disk...";
390 OPEN FILENAME$ FOR OUTPUT AS #2
400 J=0:FOR I=0 TO SIZE-1
410   FBUFSTR$ = FBUFSTR$ + CHR$(FBUF(I)):J=J+1
420   IF J=255 THEN PRINT #2, FBUFSTR$;:FBUFSTR$="":J=0
430 NEXT I
440 IF J>0 THEN PRINT #2, FBUFSTR$;
450 CLOSE
460 PRINT "Complete!"
470 END
480 ' --- Subroutines ---
490 'Checksum()
500 SUM=0
510 FOR I=0 TO 129:SUM=SUM+SBUF(I):NEXT I
520 VALID=((SUM MOD 256)=SBUF(130))
530 RETURN
540 ' Read header
550 FILENAME$=""
560 FOR I=3 TO SBUF(2)+2:FILENAME$=FILENAME$+CHR$(SBUF(I)):NEXT I
570   SIZE = CVI(CHR$(SBUF(SBUF(2)+3))+CHR$(SBUF(SBUF(2)+4)))
580   PRINT "Receiving '"FILENAME$"' ("SIZE" bytes)...";
590   OFFSET=SBUF(2)+5
600 RETURN

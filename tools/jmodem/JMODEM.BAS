10 CLS:PRINT "JMODEM by Joshua Foster"
30 DEFINT A-Z
50 DIM FBUF(16384),SBUF(131)
70 NF=0:TOTALPAKS=0
80 PRINT "Opening serial port...";
90 OPEN "COM1:1200,N,8,1,CS,DS,CD" AS #1
100 PRINT "Waiting for data..."
110 ' Receive packet
120 NS=0
130 WHILE NS<131
140   IF EOF(1) THEN 140
150   A$=INPUT$(LOC(1),#1)
160   FOR I=0 TO LEN(A$)-1
170     SBUF(NS+I)=ASC(MID$(A$,I+1,1))
180   NEXT I
190   NS=NS+I
200 WEND
202 REM FOR I=126 TO 130: PRINT SBUF(I);: NEXT I
210 ' Verify checksum
220 GOSUB 390
230 IF NOT VALID THEN PRINT "Checksum failed, trying again";:PRINT #1,CHR$(21);:GOTO 110
240 ' Verify packet number
250 PAKNUM=SBUF(1)
260 IF PAKNUM<>TOTALPAKS THEN PRINT "Expected packet"TOTALPAKS"but received"PAKNUM"!":CLOSE:END
261 OFFSET=0
262 ' Special handling for header packet
263 IF PAKNUM=0 THEN GOSUB 470
270 ' Copy packet data to filebuf ---
275 TOCOPY=SIZE-NF:IF TOCOPY>(130-OFFSET) THEN TOCOPY=(130-OFFSET)
280 FOR I=0 TO TOCOPY
290   FBUF(NF+I)=SBUF(OFFSET+I)
300 NEXT I
310 TOTALPAKS=TOTALPAKS+1
320 NF=NF+TOCOPY
330 PRINT NF"...";
340 'Ack the packet and recv the next one
350 PRINT #1,CHR$(6);
351 IF NF<SIZE THEN GOTO 110
352 PRINT "Writing file to disk...";
353 OPEN FILENAME$ FOR OUTPUT AS #2
355 FOR I=0 TO SIZE-1: FBUFSTR$ = FBUFSTR$ + CHR$(FBUF(I)): NEXT I
356 PRINT #2, FBUFSTR$
369 CLOSE
370 PRINT "Complete!"
371 END
380 ' --- Subroutines ---
390 'Checksum()
400 SUM=0
410 FOR I=0 TO 129:SUM=SUM+SBUF(I):NEXT I
450 VALID=((SUM MOD 256)=SBUF(130))
460 RETURN
470 ' Read header
490 FILENAME$=""
500 FOR I=3 TO SBUF(2)+2:FILENAME$=FILENAME$+CHR$(SBUF(I)):NEXT I
530 SIZE = CVI(CHR$(SBUF(SBUF(2)+3))+CHR$(SBUF(SBUF(2)+4)))
540 PRINT "Receiving '"FILENAME$"' ("SIZE" bytes)...";
560 OFFSET=SBUF(2)+5
570 RETURN